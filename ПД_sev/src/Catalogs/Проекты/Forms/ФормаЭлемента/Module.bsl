
&НаКлиенте
Процедура ОбновитьДетализациюПоПроекту(Команда)
	ОбновитьОтчетПоПроекту();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если Объект.Ссылка.Пустая() Тогда
		ЗаполнитьШапкуНовогопроекта();
	Иначе
		//ОбновитьОтчетПоПроекту();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьШапкуНовогопроекта()
	Объект.ДатаНачала = ТекущаяДата();
	Объект.ДатаВыполнения = КонецМесяца(ТекущаяДата());
	Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыПроектов.Анализ");
	Объект.Организация = ОбщегоНазначенияПовтИсп.ПолучитьЗначениеКонстанты("УПОсновнаяОрганизация");
	Объект.Куратор = ОбщегоНазначенияПовтИсп.ПолучитьТекущегоСотрудника();
КонецПроцедуры // ЗаполнитьШапкуНовогопроекта()

// Обновляет отчет по ссылке на проект, выводит детализацию ркгистра "ПрибыльПоПроектам" 
// по регистраторам
&НаКлиенте
Процедура ОбновитьОтчетПоПроекту()
	//ОтчетПоПроекту.Очистить();
	Если Объект.Ссылка.Пустая() Тогда
		Возврат
	КонецЕсли;
	МассивструктурОтбор = Новый Массив;
	МассивструктурОтбор.Добавить(Новый Структура("ЛевоеЗначение, ВидСравнения, ПравоеЗначение", "Проект", "Равно", Объект.Ссылка));
	стрТабДок = УправлениеОтчетамиСервер.ПолучитьТабДокИзОтчетаСКД("РентабельностьПоПроекту", "СКДДляПрограммногоФормирования",, МассивструктурОтбор, УникальныйИдентификатор); 
	ОтчетПоПроекту = стрТабДок.ТабДок;
	АдресДанныеРасшифровки = стрТабДок.АдресДанныеРасшифровки;

КонецПроцедуры // ОбновитьОтчетПоПроекту()

&НаКлиенте
Процедура ОтчетПоПроектуОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ЗначениеРасшифровки = УправлениеОтчетамиСервер.ВернутьЗначениеРасшифровки(Расшифровка, АдресДанныеРасшифровки);
	//Сообщить(ЗначениеРасшифровки);
	Если Не ЗначениеРасшифровки = Неопределено Тогда
		ПоказатьЗначение(, ЗначениеРасшифровки);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОтчетПоПроектуВыбор(Элемент, Область, СтандартнаяОбработка)
	#Если ВебКлиент Тогда 
	//Сообщить("Обработка выбора, если вы его видите сообщите разработчику системы!");
	#КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура ОтчетПоПроектуОбработкаДополнительнойРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	#Если ВебКлиент Тогда 
	СтандартнаяОбработка = Ложь;
	ЗначениеРасшифровки = УправлениеОтчетамиСервер.ВернутьЗначениеРасшифровки(Расшифровка, АдресДанныеРасшифровки);
	//Сообщить(ЗначениеРасшифровки);
	Если Не ЗначениеРасшифровки = Неопределено Тогда
		ПоказатьЗначение(, ЗначениеРасшифровки);
	КонецЕсли;
	#КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьДобавлениеДокументаКПроекту(Результат, ДополнительныеПараметры) Экспорт 
	
	Если Результат = Неопределено Тогда
		Возврат
	КонецЕсли;
	СерверСвязатьПроектСДокументом(Результат);	
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПрочиеЗатраты(Команда)
	Отбор = Новый Структура;
	Отбор.Вставить("Проект", ПредопределенноеЗначение("Справочник.Проекты.ПустаяСсылка"));	
	КлиентОткрытьФормуВыбораДокумента("ПрочиеЗатраты", Отбор);
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьРабочееВремя(Команда)
	Отбор = Новый Структура;
	Отбор.Вставить("Проект", ПредопределенноеЗначение("Справочник.Проекты.ПустаяСсылка"));	
	КлиентОткрытьФормуВыбораДокумента("РабочееВремя", Отбор);
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСчетПокупателя(Команда)
	Отбор = Новый Структура;
	Отбор.Вставить("Проект", ПредопределенноеЗначение("Справочник.Проекты.ПустаяСсылка"));
	Отбор.Вставить("Контрагент", Объект.Контрагент);
	КлиентОткрытьФормуВыбораДокумента("СчетПокупателя", Отбор);
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСчетПоставщика(Команда)
	Отбор = Новый Структура;
	Отбор.Вставить("Проект", ПредопределенноеЗначение("Справочник.Проекты.ПустаяСсылка"));
	//Отбор.Вставить("Контрагент", Объект.Контрагент);
	КлиентОткрытьФормуВыбораДокумента("СчетПоставщика", Отбор);
КонецПроцедуры

&НаКлиенте
Процедура КлиентОткрытьФормуВыбораДокумента(ИмяДокумента, Отбор = Неопределено)
	Если Объект.Ссылка.Пустая() Тогда
		Сообщить("Документ можно добавлять только в записанный проект!", СтатусСообщения.Важное);
		Возврат
	КонецЕсли;
	Оповещение = Новый ОписаниеОповещения("ОбработатьДобавлениеДокументаКПроекту", ЭтотОбъект);
	Режим = РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс;
	стрПараметры = Новый Структура;	
	Если Не Отбор = Неопределено Тогда
		стрПараметры.Вставить("Отбор", Отбор);
	КонецЕсли;
	ОткрытьФорму("Документ." + ИмяДокумента + ".ФормаВыбора",стрПараметры,ЭтаФорма,,,,Оповещение,Режим);
КонецПроцедуры

&НаСервере
Процедура СерверСвязатьПроектСДокументом(Документ)
	ОбъектДокумент = Документ.ПолучитьОбъект();
	ОбъектДокумент.Проект = Объект.Ссылка;
	ОбъектДокумент.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
КонецПроцедуры

&НаКлиенте
Процедура СтатусПриИзменении(Элемент)
	Если Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыПроектов.Завершен") Тогда
		Объект.Завершен = Истина;
	Иначе
		Объект.Завершен = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Перем стрПараметры, Проект;
	Если ИмяСобытия = "ЗаписьНаФорме" Тогда
		Если Параметр.Свойство("Параметры", стрПараметры) 
			И стрПараметры.Свойство("Проект", Проект) Тогда
			Если Проект = Объект.Ссылка Тогда
				ОбновитьОтчетПоПроекту();		
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	//Вставить содержимое обработчика
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	РаботаСФормамиКлиент.ОповеститьПослеЗаписиФормы("ЗаписьНаФормеПроекта", Объект.Ссылка);
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Не РольДоступна("ПолныеПрава") Тогда
		ТолькоПросмотр = Объект.Завершен
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ГруппаСтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница.Имя = "ГруппаИтогПОПроекту" Тогда
		ОбновитьОтчетПоПроекту();	
	КонецЕсли;
КонецПроцедуры
