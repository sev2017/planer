
&НаСервере
Функция ОткрытьФайлНаСервере(Ссылка)
	ИмяФайла = Ссылка.ИмяФайла;
	АдресХранилища = ПоместитьВоВременноеХранилище(Ссылка.Хранилище.Получить(), УникальныйИдентификатор);
	Возврат новый Структура("ИмяФайла, АдресХранилища", ИмяФайла, АдресХранилища);
КонецФункции

&НаКлиенте
Процедура ОткрытьФайл(Команда)
	ТекСтрока = Элементы.Список.ТекущиеДанные;
	Если ТекСтрока = Неопределено Тогда
		Возврат
	КонецЕсли;
	стрФайл = ОткрытьФайлНаСервере(ТекСтрока.Ссылка);
	ИмяФайла = стрФайл.ИмяФайла;
	АдресХранилища = стрФайл.АдресХранилища; 
	ПолучитьФайл(АдресХранилища, ИмяФайла, Истина);	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Список.Параметры.УстановитьЗначениеПараметра("Объект", Параметры.Объект);
	Объект = Параметры.Объект;
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьФайлНаСервере(АдресФайла, ВыбранноеИмяФайла)
	Если АдресФайла = Неопределено Тогда
		Возврат	
	КонецЕсли;
	
	Файл = Новый Файл(ВыбранноеИмяФайла);
	ИмяФайла = Файл.Имя;
	Расширение = Файл.Расширение;
	НовыйЭлемент = Справочники.ХранилищеПрикрепленныхФайлов.СоздатьЭлемент();
	НовыйЭлемент.Объект = Объект; 
	НовыйЭлемент.Наименование = ИмяФайла;
	НовыйЭлемент.ИмяФайла = ИмяФайла;
	НовыйЭлемент.Расширение = Расширение;
	НовыйЭлемент.Хранилище = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(АдресФайла), Новый СжатиеДанных(9));
	НовыйЭлемент.Записать();
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьФайл(Команда)
	Перем ВыбранноеИмяФайла, АдресФайла;
	ПоместитьФайл(АдресФайла,,ВыбранноеИмяФайла, Истина, УникальныйИдентификатор);
	ЗагрузитьФайлНаСервере(АдресФайла, ВыбранноеИмяФайла);
	Элементы.Список.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	ТекСтрока = Элементы.Список.ТекущиеДанные;
	Если Не ТекСтрока = Неопределено Тогда
		ПолучитьКартинкуСервер(ТекСтрока.Ссылка);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПолучитьКартинкуСервер(Ссылка)
	Изображение = ПолучитьНавигационнуюСсылку(Ссылка, "Хранилище") 
КонецПроцедуры
