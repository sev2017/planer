
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПолучитьТаблицуШаблоновТелефоновНаСервере(Константы.ШаблоныТелефонныхНомеров.Получить());
КонецПроцедуры


// Процедура преобразовывает хранилище значений в таблицу значений 
//
// Параметры
//  Шаблоны – хранилище значений
//
// Возвращаемое значение:
//   НЕТ
//
&НаСервере
Процедура ПолучитьТаблицуШаблоновТелефоновНаСервере(Шаблоны)

	Если ТипЗнч(Шаблоны) <> Тип("ХранилищеЗначения") Тогда
		Возврат;
	КонецЕсли;

	СтруктураШаблонов = Шаблоны.Получить();

	ШаблоныТелефонов.Очистить();

	Если ТипЗнч(СтруктураШаблонов) = Тип("Соответствие") Тогда

		Для Индекс = 0 По 50 Цикл //думаю, больше 50-ти значных номеров не может быть

			НайденныйШаблон = СтруктураШаблонов.Получить(Индекс);

			Если НайденныйШаблон <> Неопределено Тогда

				СтрокаШаблонов = ШаблоныТелефонов.Добавить();
				СтрокаШаблонов.ШаблоныТелефонныхНомеров = НайденныйШаблон;

			КонецЕсли;

		КонецЦикла;

	КонецЕсли;

КонецПроцедуры

// Функция преобразовывает таблицу значений в хранилище значения
//
// Параметры
//  Шаблоны – таблица значений
//
// Возвращаемое значение:
//   Хранилище значений
//
&НаСервере
Функция ПолучитьШаблоныНомеровНаСервере(Шаблоны)

	ТаблицаСоответствий = Новый Соответствие;

	Для каждого СтрокаШаблонов Из Шаблоны Цикл

		Если ПустаяСтрока(СтрокаШаблонов.ШаблоныТелефонныхНомеров) Тогда
			Продолжить;
		КонецЕсли;

		ТаблицаСоответствий.Вставить(СтрЧислоВхождений(СокрЛП(СтрокаШаблонов.ШаблоныТелефонныхНомеров),"9"),СокрЛП(СтрокаШаблонов.ШаблоныТелефонныхНомеров));

	КонецЦикла;

	Возврат Новый ХранилищеЗначения(ТаблицаСоответствий);

КонецФункции // ПолучитьШаблоныНомеров()

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Константы.ШаблоныТелефонныхНомеров.Установить(ПолучитьШаблоныНомеровНаСервере(ШаблоныТелефонов));	
КонецПроцедуры
