Процедура ЗаполнитьОбъектПоПроекту(ОбъектЗаполнения, Проект) Экспорт 
	МассивЗаполняемыхРеквизитов = Новый Массив;
	МассивЗаполняемыхРеквизитов.Добавить("Организация");
	МассивЗаполняемыхРеквизитов.Добавить("Контрагент");
	Для Каждого Реквизит Из МассивЗаполняемыхРеквизитов Цикл 
		Если Не ОбъектЗаполнения.Метаданные().Реквизиты.Найти(Реквизит) = Неопределено Тогда
			ОбъектЗаполнения[Реквизит] = Проект[Реквизит];	
		КонецЕсли;	
	КонецЦикла;
	ОбъектЗаполнения.Проект = Проект; 	
	Если ТипЗнч(ОбъектЗаполнения) = Тип("ДокументОбъект.СчетПокупателя") Тогда
		ЗаполнитьТабличнуюЧастьСчетаПоПроекту(ОбъектЗаполнения, Проект);
	КонецЕсли;
КонецПроцедуры

Процедура ЗаполнитьТабличнуюЧастьСчетаПоПроекту(СчетНаОплату, Проект)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПрочиеЗатратыТовары.Наименование КАК Наименование,
	               |	ПрочиеЗатратыТовары.Количество КАК Количество,
	               |	ПрочиеЗатратыТовары.Цена КАК Цена,
	               |	ПрочиеЗатратыТовары.Сумма КАК Сумма,
	               |	ЛОЖЬ КАК Услуга,
	               |	100 КАК Наценка
	               |ИЗ
	               |	Документ.ПрочиеЗатраты.Товары КАК ПрочиеЗатратыТовары
	               |ГДЕ
	               |	ПрочиеЗатратыТовары.Ссылка.Проведен
	               |	И ПрочиеЗатратыТовары.Ссылка.Проект = &Проект
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	МАКСИМУМ(""Работы""),
	               |	МАКСИМУМ(1),
	               |	СУММА(РабочееВремя.СуммаДокумента),
	               |	СУММА(РабочееВремя.СуммаДокумента),
	               |	ИСТИНА,
	               |	100
	               |ИЗ
	               |	Документ.РабочееВремя КАК РабочееВремя
	               |ГДЕ
	               |	РабочееВремя.Проведен
	               |	И РабочееВремя.Проект = &Проект
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	СчетПоставщикаТовары.Наименование,
	               |	СчетПоставщикаТовары.Количество,
	               |	СчетПоставщикаТовары.Цена,
	               |	СчетПоставщикаТовары.Сумма,
	               |	ЛОЖЬ,
	               |	100
	               |ИЗ
	               |	Документ.СчетПоставщика.Товары КАК СчетПоставщикаТовары
	               |ГДЕ
	               |	СчетПоставщикаТовары.Проект = &Проект
	               |	И СчетПоставщикаТовары.Ссылка.Проведен";
	Запрос.УстановитьПараметр("Проект", Проект); 				   
	СчетНаОплату.Товары.Загрузить(Запрос.Выполнить().Выгрузить());	
КонецПроцедуры
